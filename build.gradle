wrapper {
    gradleVersion = '5.2.1'
}

allprojects {
    group = pGroup
    version = pVersion
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'eclipse'

    repositories {
        mavenCentral()
    }
}

subprojects {
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives sourcesJar
    }
}

if (project.hasProperty("topobyte")) {
    subprojects {
        apply from: 'file:///' + System.getProperty("user.home") \
            + '/.gradle/topobyte.gradle'

        def installer = install.repositories.mavenInstaller
        def deployer = uploadArchives.repositories.mavenDeployer

        [installer, deployer]*.pom*.whenConfigured { pom ->
            pom.dependencies.removeAll { it.scope == "runtime" }
        }
    }
}

project(':luqe-core') {
    dependencies {
        compile "org.slf4j:slf4j-api:1.6.1"
        runtime "org.slf4j:slf4j-log4j12:1.6.1"
        runtime "log4j:log4j:1.2.16"
    }
}

project(':luqe-jdbc') {
    dependencies {
        compile project(':luqe-core')
    }
}

project(':luqe-android') {
    dependencies {
        compile project(':luqe-core')
        compile 'com.google.android:android:4.1.1.4'
    }
}
