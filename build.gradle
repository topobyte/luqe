task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}

allprojects {
    group = pGroup
    version = pVersion
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'eclipse'

    repositories {
        maven {
            url maven_path_topobyte
        }
        mavenCentral()
    }
}

subprojects {
    uploadArchives {
        repositories {
            mavenDeployer {
                repository(url: maven_url_topobyte)
            }
        }
    }
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives sourcesJar
    }
}

subprojects {
    def installer = install.repositories.mavenInstaller
    def deployer = uploadArchives.repositories.mavenDeployer

    [installer, deployer]*.pom*.whenConfigured { pom ->
        pom.dependencies.removeAll { it.scope == "runtime" }
    }
}

project(':luqe-core') {
    dependencies {
        compile "org.slf4j:slf4j-api:1.6.1"
        runtime "org.slf4j:slf4j-log4j12:1.6.1"
        runtime "log4j:log4j:1.2.16"
    }
}

project(':luqe-jdbc') {
    dependencies {
        compile project(':luqe-core')
    }
}

project(':luqe-android') {
    dependencies {
        compile project(':luqe-core')
        compile 'com.google.android:android:4.1.1.4'
    }
}
